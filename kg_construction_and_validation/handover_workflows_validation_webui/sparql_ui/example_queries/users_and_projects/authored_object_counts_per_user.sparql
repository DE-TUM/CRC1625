# Get the count of MLs/Samples that the user has authored in the RDMS
#
# Note that, since we want their number, we must include a COUNT variable. This requires that we
# add a GROUP BY clause at the end of the query (explained below)
#
SELECT ?user ?name (COUNT(?item) AS ?itemCount)
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
  # A user and its name
  # Class that all persons belong to, note that we can exclude it in this query as there
  # are no other entities with crc:name, etc.
  ?user a crc:User.
  # Its name
  ?user crc:name ?name.

  # *Optionally*, they may have authored Materials Libraries / Samples
  #
  # If we don't wrap this around an OPTIONAL, then those who haven't authored
  # anything won't appear in the results
  OPTIONAL {
    ?item crc:creator ?user.
    # a/rdfs:subClassOf* is a shorthand for "anything that is either
    # a Materials Library *or* a *subclass* of Materials Library (Samples...)"
    ?item a/rdfs:subClassOf* crc:MaterialsLibrary.
  }
}
# Since we want to do a COUNT of the measurements authored by each user, we need to
# indicate the variables we want to group (also known as aggregate) the results by.
#
# In this case, we group by all of them except for the COUNTed variable
GROUP BY ?user ?name
# And order them based on that count, in DESCending order
ORDER BY DESC(?itemCount)
