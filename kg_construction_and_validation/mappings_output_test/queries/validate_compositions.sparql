# Ensures that MLs 1 and 2 have 342 unique MAs, 2 unique compositions and 2 unique composition elements, each with sound metadata.
SELECT ?ML ?ma ?edx_composition (COUNT(?composition_element) AS ?comp_elements) WHERE {
    GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph> {
        ?ML a pmdco:EngineeredMaterial.
        ?ma a crc:MeasurementArea.

        ?ML crc:composedOf ?ma.

    	?edx_composition a crc:VolumeComposition.
        ?ma crc:characteristic ?edx_composition.

        FILTER NOT EXISTS { # The compositions should only be attached to one measurement
            ?other_ma a crc:MeasurementArea.
            ?other_ML a pmdco:EngineeredMaterial.

            ?other_ML crc:composedOf ?ma.
            FILTER(?other_ML != ?ML)
        }

        FILTER NOT EXISTS { # The compositions are unique too
            ?other_ma a crc:MeasurementArea.

            ?other_ma crc:characteristic ?edx_composition.
            FILTER(?other_ma != ?ma)
        }

        ?edx_measurement a/rdfs:subClassOf pmdco:ValueObject.
        ?edx_measurement crc:characteristic ?edx_composition.
        ?edx_composition crc:characteristic ?composition_element.

        FILTER NOT EXISTS { # The compositions should only be attached to one measurement
            ?other_measurement a/rdfs:subClassOf pmdco:ValueObject.
            ?other_measurement crc:characteristic ?edx_composition.

            FILTER(?other_measurement != ?edx_measurement)
        }

        FILTER NOT EXISTS { # The composition elements should also be unique
            ?other_comp a crc:VolumeComposition.
            ?other_comp crc:characteristic ?composition_element.
            FILTER(?other_comp != ?edx_composition)
        }

        # Tolerance and positions are always hardcoded to the same value
        ?edx_composition crc:tolerance "3.0"^^xsd:float.
        ?edx_composition crc:x_position "1.0"^^xsd:float.
        ?edx_composition crc:y_position "2.0"^^xsd:float.

        # Same for comp. element concentrations, they are always 50%
        ?composition_element crc:value 5e+01.
    }
} GROUP BY ?ML ?ma ?edx_composition