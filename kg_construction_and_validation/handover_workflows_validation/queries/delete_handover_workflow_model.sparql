DELETE {
    GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph/workflows> {
        ?s ?p ?o.
    }
}
WHERE {
  GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph/workflows> {
    # Immediate neighborhood (outgoing)
    {
      <{handover_workflow_model_iri}> ?p ?o.
      BIND(<{handover_workflow_model_iri}> AS ?s).
    }
    #UNION
    # Immediate neighborhood (incoming)
    # Note that, if we do this, we will unlink workflow model instances from it
    # If the workflow model is getting fully deleted and not replaced, then all instances will need
    # to be deleted separately
    #{
    #  ?s ?p <{handover_workflow_model_iri}>.
    #  BIND(<{handover_workflow_model_iri}> AS ?o).
    #}

    # Handover workflow steps (outgoing)
    UNION
    {
      <{handover_workflow_model_iri}> pmdco:subordinateProcess/pmdco:nextProcess* ?s.
      ?s ?p ?o.
    }

    # Handover workflow steps (incoming)
    # Note that, if we do this, we will delete in the process all assignments from workflow instances
    # Due to this, we clean them up or fully delete them at a later step when overwriting
    #UNION
    #{
    #  <{handover_workflow_model_iri}> pmdco:subordinateProcess/pmdco:nextProcess* ?o.
    #  ?s ?p ?o.
    #}

    # Handover workflow step activities
    UNION
    {
      <{handover_workflow_model_iri}> pmdco:subordinateProcess/pmdco:nextProcess* ?step.
      ?step pmdco:subordinateProcess ?s.
      ?s ?p ?o.
    }
  }
}
