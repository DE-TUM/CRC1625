DELETE {
  GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph> {
    ?workflow_instance crc:substep ?initial_work_handover.
  }
}
INSERT {
  GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph> {
    ?handover_group a crc:HandoverGroup.
    ?handover_group crc:substep ?initial_work_handover.
    ?handover_group crc:objectName ?handover_group_label.
    ?handover_group crc:assignedTo ?project.

    ?workflow_instance crc:substep ?handover_group.
  }
} WHERE {
  GRAPH <https://crc1625.mdi.ruhr-uni-bochum.de/graph> {
    ?workflow_instance crc:input ?sample.
    ?workflow_instance crc:substep ?initial_work_handover.

    ?initial_work_handover a crc:VirtualHandover.

    # Required metadata for the handover group
    ?sample crc:internalID ?sample_id.
    ?initial_work_handover crc:assignedTo ?user.
    ?user crc:memberOfProject ?project.
    ?project crc:objectName ?project_label.

    BIND(IRI(CONCAT(
        "https://crc1625.mdi.ruhr-uni-bochum.de/handover/handovers_in_project_",
        STR(?project_label),
        "_initial_work_for_ML_",
        STR(?sample_id)
    )) AS ?handover_group)

    BIND(CONCAT(
        "Initial work handover group within the ",
        STR(?project_label),
        " project"
    ) AS ?handover_group_label)
  }
}