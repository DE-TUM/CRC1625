SELECT ?start ?start_id ?start_date ?end ?end_id ?group ?sample_id
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
     # Two or more handovers, where both are non-virtual handovers
     {
        # There any intermediate handovers in the way, or none
        ?start crc:nextStep+ ?end .

        # start is a handover and thus has an ID
        ?start crc:internalID ?start_id.
        ?end crc:internalID ?end_id.

        ?start crc:creationDate ?start_date.

        ?start crc:assignedTo ?user_start.
        ?user_start crc:memberOfProject ?group.

        # Ensure they are the start and end of the chain
        MINUS {
            ?hnd_before_start crc:nextStep ?start.
        }

        MINUS {
            ?end crc:nextStep ?hnd_after_end.
        }
    }

    UNION

  	# Two or more handovers, where the first handover is an initial work handover
    {
        # There any intermediate handovers in the way, or none
        ?start crc:nextStep+ ?end .

        # start It's an initial work handover and thus has no ID, but we can get the sample it belongs to
        ?workflow_instance crc:substep ?start.
        ?workflow_instance crc:input ?materials_library.
        ?materials_library crc:internalID ?sample_id.

        ?end crc:internalID ?end_id.

        ?start crc:creationDate ?start_date.

        ?start crc:assignedTo ?user_start.
        ?user_start crc:memberOfProject ?group.

        # Ensure they are the start and end of the chain
        MINUS {
            ?hnd_before_start crc:nextStep ?start.
        }

        MINUS {
            ?end crc:nextStep ?hnd_after_end.
        }
    }

    UNION

  	# Isolated handovers, virtual or not
    {
    	VALUES ?start_type { crc:Handover crc:VirtualHandover }

        ?hnd a ?start_type.

        # start It's an initial work handover and thus has no ID, but we can get the sample it belongs to
        OPTIONAL {
      		?workflow_instance crc:substep ?hnd .
        	?workflow_instance crc:input ?materials_library.
        	?materials_library crc:internalID ?sample_id.
  		}

      	OPTIONAL {
  			?hnd crc:internalID ?hnd_id
  		}


        ?hnd crc:creationDate ?hnd_date.

        ?hnd crc:assignedTo ?user_hnd.
        ?user_hnd crc:memberOfProject ?group.

        # Ensure that it is isolated
        MINUS {
            ?hnd_before crc:nextStep ?hnd.
        }

        MINUS {
            ?hnd crc:nextStep ?hnd_after.
        }

    	BIND(?hnd AS ?start)
    	BIND(?hnd AS ?end)

    	BIND(?hnd_id AS ?start_id)
    	BIND(?hnd_id AS ?end_id)

    	BIND(?hnd_date AS ?start_date)
    }
}