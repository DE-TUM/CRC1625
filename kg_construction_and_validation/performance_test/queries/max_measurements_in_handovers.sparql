SELECT (MAX(?n_measurements) AS ?max_measurements_in_handovers) FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph> WHERE {
    SELECT ?handover (COUNT(?measurement) AS ?n_measurements) WHERE {
        ?sample_w_handover a crc:MaterialsLibrary.

        ?workflow_instance crc:input ?sample_w_handover.
        ?workflow_instance crc:substep ?first_handover_group.


        ?first_handover_group crc:nextStep*/crc:substep* ?handover.
        ?handover crc:substep ?activity.
        ?activity crc:output ?measurement.

        # Exclude EDX compositions, we only consider the CSV file
        FILTER NOT EXISTS {
            ?measurement a crc:VolumeComposition.
        }
    } GROUP BY ?handover
}