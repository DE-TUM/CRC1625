SELECT
  (COUNT(?sample_w_idea) as ?count_w)
  (COUNT(?sample) as ?total_samples)
  (xsd:decimal(COUNT(?sample_w_idea)) / xsd:decimal(COUNT(?sample)) as ?chance_to_have_idea)
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
    {
        SELECT DISTINCT ?sample_w_idea WHERE {
            ?sample_w_idea a :MaterialsLibrary

            # It's a main sample
            FILTER NOT EXISTS {
                ?another_sample pmdco:composedOf ?sample_w_idea.
            }

            ?idea_or_exp_plan a :IdeaOrExperimentPlan.
            ?idea_or_exp_plan pmdco:output ?sample_w_idea.
		}
    }
    UNION
    {
        SELECT ?sample WHERE {
            ?sample a :MaterialsLibrary.

      		# It's a main sample
            FILTER NOT EXISTS {
                ?another_sample pmdco:composedOf ?sample.
            }
        }
    }
}