# Query for samples which, in a EDX measurement, they have a volume of more than 50% of gold,
# alongside metadata about the measurement and measurement area it belongs to
SELECT (COUNT(*) AS ?benchmark_count)
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
    SELECT ?ML ?measurement ?measurement_area_position ?value
    WHERE {
      ?ML crc:composedOf ?measurement_area.
      ?measurement_area crc:characteristic ?composition.
      ?measurement_area crc:value ?measurement_area_position.

      ?measurement crc:characteristic ?composition.
      ?composition crc:characteristic ?edx_composition.

      # Get the compositon for gold
      ?edx_composition crc:element ?chebi_element.
      ?chebi_element chebi:formula "Au".

      # Get its value and filter for those higher than 50%
      ?edx_composition crc:value ?value.
      FILTER(?value > 50.0)

      ?ML a/rdfs:subClassOf* crc:EngineeredMaterial. # ML or sample
      ?measurement_area a crc:MeasurementArea.
      ?composition a crc:VolumeComposition.
    }
}