# Query that retrieves *all* measurements of a ML/Sample alongside their
# provenance records (i.e., where is the measurement at within the handover records)
#
# This query pattern can be used as a "give me everything" query to which we can add additional
# filters based on the handover chain, the measurement types, its values, identifiers of objects, etc.
SELECT ?MLOrSample ?measurement ?handover_group ?handover_of_group ?activity
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
    # As in the other query in this category, we follow the workflow from
    # top-to-bottom, as can also be visualized in the ontology schema
    #
    #     Workflow instance -> ML/Sample
    #             |
    #             v
    #     Handover groups chain
    #             |
    #             v
    # Handovers chain for every group
    #             |
    #             v
    # Activities for every handover
    #             |
    #             v
    #        Measurements <- ML/Sample
    #
    # Note that, if we don't want to query any information whatsoever from the handover chain,
    # we can express the query bottom-to-top (ML/Samples that have a measurement part of an XRD activity)
    #
    ?workflow_instance pmdco:input ?sample.
    ?workflow_instance pmdco:subordinateProcess ?first_handover_group.
    # With this pattern, we get *all* handover groups, including the first one
    ?first_handover_group pmdco:nextProcess* ?handover_group.


    ?handover_group pmdco:subordinateProcess ?first_handover_of_group.
    # The same way, to get *all* handovers of the group
    ?first_handover_of_group pmdco:nextProcess* ?handover_of_group.

    # We now go until the bottom/root of the hierarchy, until reaching the measurement itself
    ?handover_of_group pmdco:subordinateProcess ?activity.
    ?activity pmdco:output ?measurement.

    # And we get the ML/Sample to whicht the measurement corresponds
    ?MLOrSample pmdco:characteristic ?measurement.

    # And narrow down the types
    # pmdco:ValueObject is the high-level type for any kind of measurement in the
    # RMDS (i.e., an object in the RDMS that is linked to a ML/Sample that is not
    # another ML/sample, special object such as a handover, etc.). It also does not
    # include
    ?measurement a pmdco:ValueObject.
}