# Get MLs/Samples that are composed of specific elements
# This is equivalent to the periodic table-based search of the RDMS
SELECT ?MLOrSample
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
  # a/rdfs:subClassOf* is a shorthand for "anything that is either
  # a Materials Library *or* a *subclass* of Materials Library (Samples...)"
  ?MLOrSample a/rdfs:subClassOf* crc:MaterialsLibrary.
  ?MLOrSample crc:element ?element_1, ?element_2.

  # The elements are CheBI entities, which we can query for their formulas. In this case,
  # we say that one of the elements is gold, and the other silver
  ?element_1 chebi:formula "Ag".
  ?element_2 chebi:formula "Au".

  # Note that we also allow MLs/Samples with other elements among them. See the exclusive variant
  # of the query to force to retrieve ML/Samples of silver *and* gold, and no other elements
}
