# Get MLs/Samples that are *not* composed of specific elements
# This is equivalent to the periodic table-based search of the
# RDMS, but on the inverse
SELECT ?MLOrSample
FROM <https://crc1625.mdi.ruhr-uni-bochum.de/graph>
WHERE {
  # a/rdfs:subClassOf* is a shorthand for "anything that is either
  # a Materials Library *or* a *subclass* of Materials Library (Samples...)"
  ?MLOrSample a/rdfs:subClassOf* :MaterialsLibrary.

  # This is a very simple way of expressing negations in SPARQL. Everything inside
  # a MINUS essentially means that we *don't* want results (i.e., MLs/Samples) that
  # are composed of either of the non-desired elements
  MINUS {
    ?MLOrSample pmdco:relatesToElement ?element.
    ?element chebi:formula ?f.
    FILTER(?f IN ("Ag", "Au")) # This expresses that we want those elements to be silver or gold
  }
}
